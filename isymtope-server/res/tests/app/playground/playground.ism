use html;

store {
    let files = [
        {id: "playground.ism", name: "playground.ism"},
        {id: "app.js", name: "app.js", language: "javascript"},
        {id: "app.css", name: "app.css", language: "css"}
    ];

    let editors = [];
    let workspaceId = "1czxy";
    let activeFilename = "playground.ism";

    workspaceId {
        action chooseWorkspace(id) => id;
    }

    files {
    }

    editors {
    }

    activeFilename {
        action chooseFile(name) => name;
    }

    extern editorContent;
}

component workspaceHeader(id) {
    ("Workspace: ") strong() { (id) }
}

component file(fileData) {
    li() { 
        a(href="#") || { 
            dispatch chooseFile(name: fileData.name) to activeFilename;
            dispatch load(name: fileData.name) to editorContent;
        }
        {
            ( fileData.name )
        }
    }
}

component files(files) {
    ul() {
        file(for fileData in files) {}
    }
}

component tabs(editors) {

}

component editor(activeFilename) {
    div(class="container") {
        div(class="row") {
            div(class="column") { (activeFilename) }
        }

        div(class="row") {
            div(class="column") {
                div(id="editorComponent") {
                    extern {
                        div(id="editor") {}
                    }
                }
            }
        }
    }
}

component preview() {
    div(class="container") {
        div(class="row") {
            div(class="column") { ("Application preview:") }
        }

        div(class="row") {
            div(class="column") {
                div(id="previewComponent") {
                    iframe(id="preview", src="http://localhost:3000/?") {}
                }
            }
        }
    }
}

route "/" || { dispatch chooseWorkspace(id: "1czxy") to workspaceId; }

link(rel="stylesheet", href="https://unpkg.com/milligram@1.3.0/dist/milligram.css", type="text/css") {}
link(rel="stylesheet", href="/app.css", type="text/css") {}
script(src="https://unpkg.com/monaco-editor@0.7.0/min/vs/loader.js", type="text/javascript", defer="defer") {}
script(src="/app.js", type="text/javascript") {}

div(class="container") {
    div(class="row") {
        div(class="column column-50") {
            workspaceHeader(get workspaceId as id) {}
        }
    }

    div(class="row") {
        div(class="column") {
            files(get files) {}
        }
    }

    div(class="row") {
        div(class="column column-50") {
            tabs(get editors) {}
            editor(get activeFilename) {}
        }

        div(class="column column-50") {
            preview() {}
        }
    }
}
