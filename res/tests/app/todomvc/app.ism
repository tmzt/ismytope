use html;

store {
    let todos = [{text: "One", complete: false, id: 0}, {text: "Two", complete: true, id: 1}];
    let entry = "";

    todos {
        action add(entry) => state + (entry + {complete: false, id: (value.map(item.id).max(x) + 1)});
        action toggle_complete(id) => state | set complete = (!item.complete) where (item.id == id) |unique id;
    }

    entry {
        action clear => "";
    }
}

component todo_item (todo) {
    li (class={completed: todo.complete}) {
        div (class="view") {
            input(class="toggle", type="checkbox") bind todo.complete as complete
                change || { dispatch toggle_complete(id: todo.id); } {}
            label { (todo.text) }
            button(class="destroy") {}
        }
     }
}

component new_todo (entry) {
    input(class="new-todo", placeholder="What needs to be done?", autofocus="autofocus") bind entry
        enterkey || { dispatch add(entry: {text: entry}); } {}
}

link(rel="stylesheet", href="https://unpkg.com/todomvc-common@1.0.3/base.css", type="text/css") {}
link(rel="stylesheet", href="https://unpkg.com/todomvc-app-css@2.1.0/index.css", type="text/css") {}

section(class="todoapp") {
    header(class="header") {
        h1() { ("todos") }
        new_todo(get entry) {}
    }

    section(class="main") {
        input(class="toggle-all", id="toggle-all", type="checkbox") {}
        label(class="", for="toggle-all") { ("Mark all as complete") }
        ul(id="todo-list", class="todo-list") {
            todo_item (for todo in todos) {}
        }
    }
}